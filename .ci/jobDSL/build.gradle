apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
        }
    }
}

repositories {
    maven { url 'http://repo.jenkins-ci.org/releases/' }
    mavenCentral()
    jcenter()
}

configurations {
    testPlugins {}

    // see JENKINS-45512
    testCompile {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
    sourceCompatibility = JavaVersion.VERSION_1_10
    targetCompatibility = JavaVersion.VERSION_1_10
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.4.14'
  //compile 'org.kohsuke:github-api:1.93'

  testCompile 'org.spockframework:spock-core:1.3-groovy-2.4'

  // Jenkins test harness dependencies
  testCompile('org.jenkins-ci.main:jenkins-test-harness:2.71')
  testCompile("org.jenkins-ci.main:jenkins-war:${jenkinsVersion}")

  // Detached
  testPlugins 'org.jenkins-ci.plugins:jdk-tool:1.4'
  testPlugins 'org.jenkins-ci.plugins:trilead-api:1.0.11'
  testPlugins 'org.jenkins-ci.plugins:bouncycastle-api:2.18'

  // Job DSL plugin including plugin dependencies
  testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
  testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
  testCompile 'org.jenkins-ci.plugins:structs:1.21@jar'

  // Plugins to install in test instance
  testPlugins 'org.jenkins-ci.plugins:token-macro:2.12'
  testPlugins 'org.jenkins-ci.plugins:github-scm-trait-notification-context:1.1'
  testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.14'
  testPlugins 'org.jenkins-ci.plugins:credentials:2.3.14'
  testPlugins 'org.jenkins-ci.plugins:basic-branch-build-strategies:1.3.2'
  testPlugins 'org.jenkins-ci.plugins.workflow:workflow-multibranch:2.22'
  testPlugins 'org.jenkins-ci.plugins:git:4.4.4'
  testPlugins 'org.jenkins-ci.plugins:github-branch-source:2.9.0'

  // Run the following script in the Script Console of your Jenkins instance to generate
  // the above testPlugins list. (adapted from https://git.io/fjpUs)
  /*
      Jenkins.instance.pluginManager.plugins
          .findAll { !(it.shortName in ['job-dsl', 'structs']) }
          .collect { "testPlugins '${it.manifest.mainAttributes.getValue("Group-Id")}:${it.shortName}:${it.version}'" }
          .sort()
          .each { println it }
   */
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'

    doLast {
        def baseNames = source.collect { it.name[0..it.name.lastIndexOf('.')-1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
}
